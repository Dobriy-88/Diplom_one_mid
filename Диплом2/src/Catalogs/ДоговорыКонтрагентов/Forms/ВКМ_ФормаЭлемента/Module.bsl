#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Если Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.ВКМ_Абонент") Тогда
		Элементы.ГруппаРеквизитыАбонентскогоДоговора.Видимость = Истина;
	Иначе
		Элементы.ГруппаРеквизитыАбонентскогоДоговора.Видимость = Ложь;
	КонецЕсли;

	РеквизитыДоговораСтрокой = ВКМ_ВспомогательныеФункцииКлиентСервер.РеквизитыДоговораСтрокой(Объект.ВКМ_Номер,
		Объект.ВКМ_Дата, Объект.ВидДоговора);

	Если ПустаяСтрока(Объект.Наименование) Или (Объект.Наименование = РеквизитыДоговораСтрокой) Тогда
		МожноИзменятьНаименование = Истина;
	Иначе
		МожноИзменятьНаименование = Ложь;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	Если Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.Покупатель") ИЛИ
	Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.Поставщик") Тогда
		Объект.ВКМ_СрокДействия = "01010001000000"; 
		Объект.ВКМ_СтоимостьЧасаРаботы = 0;
		Объект.ВКМ_СуммаАбонентскойПлаты = 0;
	ИначеЕсли Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.ВКМ_Абонент") Тогда
		Если Объект.ВКМ_СрокДействия < Объект.ВКМ_Дата Тогда
			Отказ = Истина;
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = "Срок действия договора не может быть меньше даты заключения договора!";
			Сообщение.Поле = "Объект.ВКМ_СрокДействия";
			Сообщение.Сообщить();
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ВКМ_ДатаПриИзменении(Элемент)
	СформироватьНаименованиеДоговора();
КонецПроцедуры

&НаКлиенте
Процедура ВКМ_НомерПриИзменении(Элемент)
	СформироватьНаименованиеДоговора();
КонецПроцедуры

&НаКлиенте
Процедура ВидДоговораПриИзменении(Элемент)
	Если Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.ВКМ_Абонент") Тогда
		Элементы.ГруппаРеквизитыАбонентскогоДоговора.Видимость = Истина;
	Иначе
		Элементы.ГруппаРеквизитыАбонентскогоДоговора.Видимость = Ложь;		
	КонецЕсли;
	
	СформироватьНаименованиеДоговора();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура СформироватьНаименованиеДоговора()
	
	Если ПустаяСтрока(Объект.Наименование) ИЛИ МожноИзменятьНаименование Тогда
		Объект.Наименование = ВКМ_ВспомогательныеФункцииКлиентСервер.РеквизитыДоговораСтрокой(Объект.ВКМ_Номер, Объект.ВКМ_Дата, Объект.ВидДоговора);
	КонецЕсли;
			
КонецПроцедуры

#КонецОбласти